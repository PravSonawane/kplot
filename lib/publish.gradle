apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

def siteUrl = 'https://github.com/PravSonawane/kplot'
def gitUrl = 'https://github.com/PravSonawane/kplot.git'

group = GROUP_ID
version = VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION

                from components.release
                artifact androidSourcesJar

                pom {
                    name = ARTIFACT_ID
                    description = 'A Kotlin library for charts using Jetpack Compose'
                    url = siteUrl
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'pravinsonawane'
                            name = 'Pravin Sonawane'
                            email = 'june.pravin@gmail.com'
                        }
                    }

                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            System.getenv("OSS_SIGNING_KEY_ID"),
            System.getenv("OSS_SIGNING_KEY"),
            System.getenv("OSS_SIGNING_PASSWORD"),
    )
    sign publishing.publications
}